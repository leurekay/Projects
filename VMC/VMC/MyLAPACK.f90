!--------------------Linear Algebra-----------------------------------------------
FUNCTION DDET(N, A)
   IMPLICIT NONE
   INTEGER N
   REAL*8 A(N, N), DDET
   
   REAL*8 B(N, N)
   INTEGER I, INFO, IPVT(N)

   IF(N<=0)STOP 'INVALID DIM @ DDET'
   IF(N==1)THEN; DDET=A(1, 1); RETURN; END IF   
   B=A;  CALL DGETRF(N, N, B, N, IPVT, INFO)
   IF(INFO/=0)THEN; DDET=0; RETURN; END IF          
   INFO=1; DDET=0
   DO I=1, N
      IF(IPVT(I).NE.I)INFO=-INFO
	  IF(B(I, I)<0)THEN; INFO=-INFO; B(I, I)=-B(I, I); END IF
	  DDET=DDET+LOG(B(I, I))
   END DO
   DDET=EXP(DDET); IF(INFO<0)DDET=-DDET

   RETURN
END FUNCTION DDET

FUNCTION ZDET(N, A)
   IMPLICIT NONE
   INTEGER N
   COMPLEX*16 A(N, N), ZDET

   INTEGER I, INFO, IPVT(N)
   COMPLEX*16 B(N, N)

   IF(N<=0)STOP 'INVALID DIM @ ZDET'
   IF(N==1)THEN; ZDET=A(1, 1); RETURN; END IF
   B=A;  CALL ZGETRF(N, N, B, N, IPVT, INFO)
   IF(INFO/=0)THEN; ZDET=0; RETURN; END IF          
   INFO=1; ZDET=0
   DO I=1, N
      IF(IPVT(I)/=I)INFO=-INFO
	  ZDET=ZDET+LOG(B(I, I))
   END DO
   ZDET=EXP(ZDET); IF(INFO<0)ZDET=-ZDET
   
   RETURN
END FUNCTION ZDET

SUBROUTINE DINVERT(N, A)
  IMPLICIT NONE
  INTEGER N
  REAL*8 A(N, N)
  
  REAL*8 WORK(2*N)
  INTEGER INFO, IPIV(N), LWORK

  IF(N<=0)STOP 'INVALID N @ DINVERT'
  IF(N==1)THEN; A=1/A; RETURN; END IF
  CALL DGETRF(N, N, A, N, IPIV, INFO)
  IF(INFO/=0)STOP 'LU @ DINVERT DETECTED A SINGULARITY'
  LWORK=2*N
  CALL DGETRI(N, A, N, IPIV, WORK, LWORK, INFO)
  IF(INFO/=0)STOP 'DINVERT FAILED'
  
  RETURN
END SUBROUTINE DINVERT

SUBROUTINE ZINVERT(N, A)
  IMPLICIT NONE
  INTEGER N
  COMPLEX*16 A(N, N)
  
  INTEGER INFO, IPIV(N), LWORK
  COMPLEX*16 WORK(2*N)

  IF(N<=0)STOP 'INVALID DIM @ ZINVERT'
  IF(N==1)THEN; A=1/A; RETURN; END IF
  CALL ZGETRF(N, N, A, N, IPIV, INFO)
  IF(INFO/=0)PRINT*, 'LU@INVERSE DETECTED A SINGULARITY'
  LWORK=2*N 
  CALL ZGETRI(N, A, N, IPIV, WORK, LWORK, INFO)
  IF(INFO/=0)STOP 'ZINVERT FAILED'

  RETURN
END SUBROUTINE ZINVERT

SUBROUTINE DHEIGEN(N, A, EVAL)
  IMPLICIT NONE
  INTEGER N
  REAL*8 A(N, N), EVAL(N)

  INTEGER INFO
  REAL*8 WORK(3*N)

  IF(N<=0)STOP 'INVALID N @ DHEIGEN'
  IF(N==1)THEN; EVAL(1)=A(1, 1); A=1; RETURN; END IF
  CALL DSYEV('V', 'U', N, A, N, EVAL, WORK, 3*N, INFO)
  IF(INFO/=0)STOP 'DHEIGEN FAILED'

  RETURN
END SUBROUTINE DHEIGEN

SUBROUTINE ZHEIGEN(N, A, EVAL)
  IMPLICIT NONE
  INTEGER N
  COMPLEX*16 A(N, N)
  REAL*8 EVAL(N)

  !DEFINITION: 
  !    A * V = V * DIAG(EVAL)
  !SPECTRAL EXPANSION OF A:
  !    A = V * DAIG(EVAL) * V^H
  !ON EXIT, V IS STORED AS COLUMNS IN A

  INTEGER LWORK, INFO
  COMPLEX*16 RWORK(N*3), WORK(N*2)

  IF(N<=0)STOP 'INVALID N @ ZHEIGEN'
  IF(N==1)THEN; EVAL(1)=A(1, 1); A=1; RETURN; END IF

  LWORK=N*2
  !CALL CHEEV('V', 'U', N, A, N, EVAL, WORK, LWORK, RWORK, INFO)
  CALL ZHEEV('V', 'U', N, A, N, EVAL, WORK, LWORK, RWORK, INFO)
  IF(INFO/=0)STOP 'ZHEIGEN FAILED'
  RETURN

END SUBROUTINE ZHEIGEN


SUBROUTINE ZGEIGEN(N, A, VL, VR, EVAL)
  IMPLICIT NONE
  INTEGER N
  COMPLEX*16, DIMENSION(N, N) :: A, VL, VR, EVAL(N)

  ! DEFINITION OF LEFT AND RIGHT EIGEN VECTORS STORED AS COLUMNS IN VL AND VR:
  !    VL^H * A = D * VL^H,  A * VR = VR * D,  D = DIAG(EVAL)
  ! SPECTRAL EXPANSION OF A:
  !    A = VR * D * INV(VR) = INV(VL^H) * D * VL^H.
  ! IN THE ABOVE SENSE, VL^H = INV(VR), SO THAT VL AND VR ARE NOT REALLY INDEPENDENT.
  ! FOR HERMITIAN A, INV(VR) = VR^H SO THAT VL = VR.

  COMPLEX*16 WORK(2*N+1)
  REAL*8 RWORK(2*N)
  CHARACTER*1 JOBL, JOBR
  INTEGER INFO, LWORK, LDA, LDVR, LDVL

  LDA = N;  LDVL = N;  LDVR = N; LWORK = 2*N+1; JOBL = 'V'; JOBR = 'V'
  CALL ZGEEV (JOBL, JOBR, N, A, LDA, EVAL, VL, LDVL, VR, LDVR, WORK, LWORK, RWORK, INFO)

  RETURN
END SUBROUTINE ZGEIGEN




SUBROUTINE DSVD( M, N, A, U, S, V )
  IMPLICIT NONE
  INTEGER M, N
  REAL*8 A(M, N), U(M, M), S(*), V(N, N)

  INTEGER LWORK, INFO
  REAL*8, ALLOCATABLE, DIMENSION (:) :: WORK

  LWORK=MAX(1, 3*MIN(M, N) + MAX(M, N), 5*MIN(M, N) )+100
  ALLOCATE( WORK(LWORK) )
  CALL DGESVD ('A', 'A', M, N, A, M, S, U, M, V, N, WORK, LWORK, INFO )
  IF(INFO/=0) STOP 'DSVD FAILED'
  DEALLOCATE(WORK)
  RETURN

END SUBROUTINE DSVD


SUBROUTINE ZSVD( M, N, A, U, S, V )
  IMPLICIT NONE
  INTEGER M, N
  COMPLEX*16 A(M, N), U(M, M), V(N, N)
  REAL*8 S(*)

  INTEGER LWORK, INFO
  COMPLEX*8, ALLOCATABLE :: WORK(:)
  COMPLEX*16, ALLOCATABLE :: RWORK(:)

  LWORK=MAX(1, 3*MIN(M, N)+MAX(M, N), 5*MIN(M, N))+100
  ALLOCATE(WORK(LWORK), RWORK(MIN(M, N)*5))
  CALL ZGESVD ('A', 'A', M, N, A, M, S, U, M, V, N, WORK, LWORK, RWORK, INFO)
  IF(INFO/=0) STOP 'ZSVD FAILED'
  DEALLOCATE(WORK, RWORK)
  
  RETURN
END SUBROUTINE ZSVD



SUBROUTINE DQR(N, A, Q, R)
  IMPLICIT NONE
  INTEGER N
  REAL*8, DIMENSION(N, N) :: A, Q, R

  INTEGER LWORK, INFO, I
  REAL*8 TAU(N), WORK(N), COL(N, 1), ROW(1, N)

  R = A; LWORK = N
  CALL DGEQRF(N, N, R, N, TAU, WORK, LWORK, INFO)
  Q = 0; DO I = 1, N; Q(I, I) = 1; END DO; Q(N, N) = 1 - TAU(N)
  DO I = N-1, 1, -1 
     COL = 0;  COL(I, 1) = 1;   COL(I+1:N, 1) = R(I+1:N, I)
	 R(I+1:N, I) = 0D0;         ROW(1, :) = COL(:, 1)
	 Q = Q - TAU(I) * MATMUL(COL, MATMUL(ROW, Q) )
  END DO

  RETURN
END SUBROUTINE DQR


SUBROUTINE ZQR(N, A, Q, R)
  IMPLICIT NONE
  INTEGER N
  COMPLEX*16, DIMENSION(N, N) :: A, Q, R

  INTEGER LWORK, INFO, I
  COMPLEX*16 TAU(N), WORK(N), COL(N, 1), ROW(1, N)

  R = A; LWORK = N
  CALL ZGEQRF(N, N, R, N, TAU, WORK, LWORK, INFO)
  Q = 0; DO I = 1, N; Q(I, I) = 1; END DO; Q(N, N) = 1 - TAU(N)
  DO I = N-1, 1, -1 
     COL = 0D0;  COL(I, 1) = 1;            COL(I+1:N, 1) = R(I+1:N, I)
	 R(I+1:N, I) = CMPLX(0D0, 0D0);         ROW(1, :) = CONJG( COL(:, 1) )
	 Q = Q - TAU(I) * MATMUL(COL, MATMUL(ROW, Q) )
  END DO

  RETURN
END SUBROUTINE ZQR


SUBROUTINE DRQ(N, A, R, Q)
  IMPLICIT NONE
  INTEGER N
  REAL*8, DIMENSION(N, N) :: A, R, Q

  INTEGER M, LDA, LWORK, INFO, I
  REAL*8 TAU(N), WORK(N), COL(N, 1), ROW(1, N)

  M = N; LDA = N; LWORK = N; R = A
  CALL DGERQF( M, N, R, LDA, TAU, WORK, LWORK, INFO )
  Q = 0; DO I = 1, N; Q(I, I) = 1; END DO; Q(1, 1) = 1 - TAU(1)
  DO I = 2, N
     COL = 0; COL(I, 1) = 1;   COL(1:I-1, 1) = R(I, 1:I-1)
	 R(I, 1:I-1) = 0D0;        ROW(1, :) = COL(:, 1)
	 Q = Q - TAU(I) * MATMUL( MATMUL(Q, COL), ROW )
  END DO

  RETURN
END SUBROUTINE DRQ

SUBROUTINE ZRQ(N, A, R, Q)
  IMPLICIT NONE
  INTEGER N
  COMPLEX*16, DIMENSION(N, N) :: A, R, Q

  INTEGER M, LDA, LWORK, INFO, I
  COMPLEX*16 TAU(N), WORK(N), COL(N, 1), ROW(1, N)

  M = N; LDA = N; LWORK = N; R = A
  CALL ZGERQF( M, N, R, LDA, TAU, WORK, LWORK, INFO )
  Q = 0; DO I = 1, N; Q(I, I) = 1; END DO; Q(1, 1) = 1 - CONJG( TAU(1) )
  DO I = 2, N
     COL = 0; COL(I, 1) = 1;              COL(1:I-1, 1) = CONJG( R(I, 1:I-1) )
	 R(I, 1:I-1) = CMPLX(0D0, 0D0);        ROW(1, :) = CONJG( COL(:, 1) )
	 Q = Q - CONJG( TAU(I) ) * MATMUL( MATMUL(Q, COL), ROW )
  END DO

  RETURN
END SUBROUTINE ZRQ


SUBROUTINE DQL(N, A, Q, L)
  IMPLICIT NONE
  INTEGER N
  REAL*8, DIMENSION (N, N) :: A, Q, L

  INTEGER M, INFO, LDA, LWORK, I
  REAL*8 TAU(N), WORK(N), COL(N, 1), ROW(1, N)

  M = N; LDA = N; LWORK = N;  L = A
  CALL DGEQLF( M, N, L, LDA, TAU, WORK, LWORK, INFO )
  Q = 0;  DO I = 1, N; Q(I, I) = 1; END DO; Q(1, 1) = 1 - TAU(1)
  DO I = 2, N
     COL = 0;  COL(I, 1) = 1;   COL(1:I-1, 1) = L(1:I-1, I)
     L(1:I-1, I) = 0D0;         ROW(1, :) = COL(:, 1)
	 Q = Q - TAU(I) * MATMUL( COL, MATMUL(ROW, Q) )
  END DO

  RETURN
END SUBROUTINE DQL


SUBROUTINE ZQL(N, A, Q, L)
  IMPLICIT NONE
  INTEGER N
  COMPLEX*16, DIMENSION (N, N) :: A, Q, L

  INTEGER M, INFO, LDA, LWORK, I
  COMPLEX*16 TAU(N), WORK(N), COL(N, 1), ROW(1, N)

  M = N; LDA = N; LWORK = N;  L = A
  CALL ZGEQLF( M, N, L, LDA, TAU, WORK, LWORK, INFO )
  Q = 0;  DO I = 1, N; Q(I, I) = 1; END DO; Q(1, 1) = 1 - TAU(1)
  DO I = 2, N
     COL = 0;  COL(I, 1) = 1;              COL(1:I-1, 1) = L(1:I-1, I)
     L(1:I-1, I) = CMPLX(0D0, 0D0);         ROW(1, :) = CONJG( COL(:, 1) )
	 Q = Q - TAU(I) * MATMUL( COL, MATMUL(ROW, Q) )
  END DO

  RETURN
END SUBROUTINE ZQL


SUBROUTINE DLQ(N, A, L, Q)
  IMPLICIT NONE
  INTEGER N
  REAL*8, DIMENSION(N, N) :: A, L, Q

  INTEGER M, LDA, LWORK, INFO, I
  REAL*8, DIMENSION(N) :: TAU, WORK, COL(N, 1), ROW(1, N)

  M = N; LDA = N; LWORK = N; L = A
  CALL DGELQF( M, N, L, LDA, TAU, WORK, LWORK, INFO )
  Q = 0; DO I = 1, N; Q(I, I) = 1; END DO; Q(N, N) = 1 - TAU(N)
  DO I = N-1, 1, -1
     COL = 0; COL(I, 1) = 1;   COL(I+1:N, 1) = L(I, I+1:N)
	 L(I, I+1:N) = 0D0;        ROW(1, :) = COL(:, 1)
	 Q = Q - TAU(I) * MATMUL( MATMUL(Q, COL), ROW )
  END DO
   
  RETURN
END SUBROUTINE DLQ



SUBROUTINE ZLQ(N, A, L, Q)
  IMPLICIT NONE
  INTEGER N
  COMPLEX*16, DIMENSION(N, N) :: A, L, Q

  INTEGER M, LDA, LWORK, INFO, I
  COMPLEX*16 WORK(N), TAU(N), COL(N, 1), ROW(1, N)

  M = N; LDA = N; LWORK = N; L = A
  CALL ZGELQF (M, N, L, LDA, TAU, WORK, LWORK, INFO)
  Q = 0; DO I = 1, N; Q(I, I) = 1; END DO;  Q(N, N) = 1 - CONJG( TAU(N) )
  DO I = N-1, 1, -1
     COL = 0; COL(I, 1) = 1;              COL(I+1:N, 1) =  CONJG( L(I, I+1:N) )
	 L(I, I+1:N) = CMPLX(0D0, 0D0);        ROW(1, :) = CONJG( COL(:, 1) )
	 Q = Q - CONJG( TAU(I) ) * MATMUL( MATMUL(Q, COL), ROW )
  END DO 

  RETURN
END SUBROUTINE ZLQ




SUBROUTINE ZFFT(N, Z, INFO)
  IMPLICIT NONE
  INTEGER N, INFO
  COMPLEX*16 Z(N)
  
  COMPLEX*16 EXPA, ZT, ONE
  REAL*8 E, A
  INTEGER I, J, K, M, L, N1, N2

  IF(ABS(INFO)/=1)STOP 'INFO MUST BE EITHER 1 OR -1 @ ZFFT.'
  !INFO CONTROLLS THE FFT FACTOR EXP(-INFO*ONE*W*T)
  
  M=0; I=1;  DO WHILE (I<N); I=I*2; M=M+1; END DO
  IF(I/=N)STOP 'N /= 2^M @ ZFFT'
  
  !--------------MAIN FFT LOOPS----------------------
  ONE=CMPLX(0, 1); N2=N
  DO K =1, M
     N1=N2;  N2=N2/2;  E=6.283185307179586/N1;  A=0   
     DO J=1, N2;  EXPA=EXP(-INFO*ONE*A);  A=J*E
     DO I=J, N, N1;  L=I+N2
        ZT=Z(I)-Z(L);  Z(I)=Z(I)+Z(L);  Z(L)= EXPA*ZT
     END DO; END DO
  END DO
  !------------DIGIT REVERSE COUNTER-----------------
  J=1;  N1=N-1
  DO I=1, N1
     IF(I<J)THEN; ZT=Z(J); Z(J)=Z(I); Z(I)=ZT; END IF
     K=N/2;  DO WHILE(K<J);  J=J-K;  K=K/2; END DO;  J=J+K
  END DO

  RETURN
END SUBROUTINE ZFFT
